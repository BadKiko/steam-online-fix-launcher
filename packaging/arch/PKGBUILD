# Maintainer: badkiko <badkiko@badkiko.page>
pkgname=sofl
pkgver=0.0.3.1a
pkgrel=1
pkgdesc="Steam Online Fix Launcher - Launch all your games"
arch=('x86_64')
url="https://github.com/BadKiko/steam-online-fix-launcher"
license=('GPL-3.0-or-later')
depends=(
    'python>=3.8'
    'python-gobject'
    'gtk4>=4.14.0'
    'libadwaita>=1.4'
    'python-requests'
    'python-pillow'
    'python-cairo'
    'python-psutil'
    'python-pyxdg'
    'glib2'
    'gobject-introspection'
    'cairo'
    'pango'
    'gdk-pixbuf2'
    'glibc'
    'gcc-libs'
    'python-rarfile'
)
makedepends=(
    'meson'
    'ninja'
    'git'
)
optdepends=(
    'steam: For Steam game integration'
    'lutris: For Lutris game integration'
    'heroic-games-launcher: For Heroic game integration'
    'umu-launcher: For running Windows games via Proton outside Steam'
)

source=(
    "$pkgname-$pkgver.tar.gz"
    "umu-launcher-1.2.9-zipapp.tar::https://github.com/Open-Wine-Components/umu-launcher/releases/download/1.2.9/umu-launcher-1.2.9-zipapp.tar"
)
sha256sums=(
    'SKIP'
    '1be4a3ce6a7617547c19e579610fc18c9ce5c6b32b62216eabd8528bc36a136d'
)

build() {
    cd "$srcdir/steam-online-fix-launcher-$pkgver" || {
        echo "Directory $srcdir/steam-online-fix-launcher-$pkgver not found"
        ls -la "$srcdir"
        exit 1
    }
    meson setup build --prefix=/usr --buildtype=release -Dprofile=release -Dtiff_compression=webp
    meson compile -C build
}

package() {
    cd "$srcdir/steam-online-fix-launcher-$pkgver"
    meson install --destdir="$pkgdir" -C build

    # Vendor python 'vdf' module into /usr/share/sofl/vendor
    install -d "$pkgdir/usr/share/sofl/vendor"
    PYTHONUSERBASE="$pkgdir/usr/share/sofl/vendor" pip install --no-compile --no-cache-dir --no-input --disable-pip-version-check --no-warn-script-location --root "$pkgdir" --target "$pkgdir/usr/share/sofl/vendor" vdf

    # Vendor UMU launcher into package so users don't need AUR
    install -d "$pkgdir/usr/share/sofl/umu"
    if [ -f "$srcdir/umu-run" ]; then
        install -m755 "$srcdir/umu-run" "$pkgdir/usr/share/sofl/umu/umu-run"
    elif [ -f "$srcdir/umu-launcher-1.2.9-zipapp/umu-run" ]; then
        install -m755 "$srcdir/umu-launcher-1.2.9-zipapp/umu-run" "$pkgdir/usr/share/sofl/umu/umu-run"
    else
        # Fallback: attempt to locate umu-run anywhere in $srcdir
        UMU_CANDIDATE=$(find "$srcdir" -maxdepth 2 -type f -name umu-run | head -n1)
        if [ -n "$UMU_CANDIDATE" ]; then
            install -m755 "$UMU_CANDIDATE" "$pkgdir/usr/share/sofl/umu/umu-run"
        else
            echo "Warning: umu-run not found in sources; package will rely on system umu-launcher if available" >&2
        fi
    fi
}
